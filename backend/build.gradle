plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.spotbugs' version '6.0.18'
	id 'jacoco'
}

group = 'fi.vnest.speechtherapy'
version = '0.0.1-SNAPSHOT'
description = 'API for VNest based speech therapy app'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.postgresql:postgresql:42.6.0'
	implementation 'org.flywaydb:flyway-core:9.22.0'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
}

spotbugs {
	toolVersion = '4.8.4'
	excludeFilter = file("config/spotBugs/excludeFilter.xml")
}

jacoco {
	toolVersion = '0.8.11'
}

tasks.named('test') {
	useJUnitPlatform()
	ignoreFailures = true
	finalizedBy jacocoTestReport
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
	reports {
		xml.required = false
		html.required = true
	}
	ignoreFailures = false
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = true
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.70
			}
		}
	}
}
